def alertFeishu(isSuccess) {
    def status = isSuccess ? "✅" : "❌"
    def committer = sh(script: "git log -1 --pretty=format:%an", returnStdout: true).trim()
    def triggerUser = currentBuild.getBuildCauses()[0].userId
    def text = """构建提醒
    - 服务：${env.service_name}
    - 环境：稳定版测试服
    - 是否成功：${status}
    - 构建分支：${env.branch}
    - 构建人：${triggerUser}
    - 构建详情：${env.BUILD_URL}
"""
    def filename = "${env.service_name}_feishu_payload.json"
    def payload = groovy.json.JsonOutput.toJson([msg_type: 'text', content: [text: text]])
    writeFile file: "${filename}", text: payload, encoding: 'UTF-8'
    sh """
      curl -sS -X POST -H 'Content-Type: application/json' --data-binary @'${filename}' '${env.feishu_webhook_url}'
    """
}

pipeline {
    agent any
    tools {git 'Default'}

    environment {
        branch = 'main'
        SERVICE_ENV = "test_stable"
        service_name = 'backend'
        project_name = 'magi-backend-stable'
        group_id = 'Backend'
        docker_data_id = 'test_stable.yaml'
        credentials_data_id = 'credentials.json'
        image_name = "${harbor_domain}/backend/${service_name}"
        repository_url = "git@github.com:AutoGameAI/Backend.git"
        GIT_SSH_COMMAND='ssh -o StrictHostKeyChecking=no -o UserKnownHostsFile=/dev/null'
        namespace_id = '9cae255e-c01b-4fa3-bfcd-c0bb3030a565&namespaceId=9cae255e-c01b-4fa3-bfcd-c0bb3030a565'
        feishu_webhook_url = "https://open.feishu.cn/open-apis/bot/v2/hook/f45444e3-c2e5-4ea9-8b06-91204ddbeea6"
    }

    options {
        disableConcurrentBuilds()
        timeout(time: 60, unit: 'MINUTES')
    }

    stages {
        stage('Check Workspace') {
            steps {
                script {
                    echo "当前工作目录: ${WORKSPACE}"
                    sh "rm -rf ./app"
                }
            }
        }

        stage('Prepare Environment') {
            steps {
                script {
                    env._version = env.SERVICE_ENV + new Date().format('yyyyMMddHHmmss')
                    echo "构建分支：${env.branch} 版本号：${env._version}"
                }
            }
        }

        stage('Get Code') {
            steps {
                echo "拉取项目代码：${env.branch}"
                dir('app') {
                    checkout([
                        $class: 'GitSCM',
                        branches: [[name: branch]],
                        userRemoteConfigs: [[
                            url: repository_url,
                            credentialsId: 'GitHubSSH'
                        ]]
                    ])
                }
            }
        }

        stage('Build Docker Image') {
            steps {
                script {
                    try {
                        echo "构建镜像"
                        dir('app') {
                            withCredentials([
                                string(credentialsId: 'harbor_username', variable: 'HARBOR_USERNAME'),
                                string(credentialsId: 'harbor_password', variable: 'HARBOR_PASSWORD')
                            ]) {
                                sh """
                                echo "$HARBOR_PASSWORD" | docker login ${harbor_domain} -u $HARBOR_USERNAME --password-stdin
                                """
                            }
                            sh """
                            docker build -t ${image_name}:${env._version} .
                            docker push ${image_name}:${env._version}
                            docker rmi -f ${image_name}:${env._version}
                            """
                        }
                    } catch (e) {
                        error "构建镜像失败: ${e}"
                    }
                }
            }
        }

        stage('Deploy Service') {
            steps {
                script {
                    try {
                        def node = env.test_node_1
                        echo "部署服务：${node}"
                        withCredentials([
                            string(credentialsId: 'harbor_username', variable: 'HARBOR_USERNAME'),
                            string(credentialsId: 'harbor_password', variable: 'HARBOR_PASSWORD')
                        ]) {
                            sh """
                            echo "$HARBOR_PASSWORD" | ssh -o StrictHostKeyChecking=no root@${node} 'docker login ${harbor_domain} -u $HARBOR_USERNAME --password-stdin'
                            """
                        }

                        withCredentials([
                            usernamePassword(
                                credentialsId: 'NacosAccount',
                                usernameVariable: 'nacos_username',
                                passwordVariable: 'nacos_password'
                            )
                        ]) {
                            configFileProvider([configFile(
                                fileId: 'FetchConfigFromNacos',
                                targetLocation: 'fetch_nacos.sh'
                            )]) {
                                sh """
                                bash fetch_nacos.sh
                                ssh root@${node} 'mkdir -p /etc/${env.service_name}/'
                                scp -o StrictHostKeyChecking=no docker-compose.yaml root@${node}:/etc/${env.service_name}/
                                scp -o StrictHostKeyChecking=no credentials.json root@${node}:/etc/${env.service_name}/
                                rm docker-compose.yaml credentials.json
                                """
                                echo "[Nacos] 配置已推送到 ${node}"
                            }
                        }

                        sh """
                        ssh root@${node} 'echo "IMAGE_TAG=${env._version}" > /etc/${env.service_name}/.env'
                        ssh root@${node} 'docker-compose -f /etc/${env.service_name}/docker-compose.yaml -p ${env.project_name} down --remove-orphans || true'
                        ssh root@${node} 'docker-compose -f /etc/${env.service_name}/docker-compose.yaml -p ${env.project_name} up -d'
                        echo "服务部署成功：${env.service_name}"
                        """
                    } catch (e) {
                        error "服务部署失败: ${e}"
                    }
                }
            }
        }
    }

    post {
        success { script { alertFeishu(true) } }
        failure { script { alertFeishu(false) } }
    }
}